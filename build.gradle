plugins {
    id 'com.gorylenko.gradle-git-properties' version '2.2.1'
    id 'org.springframework.boot' version '2.2.4.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'com.github.ben-manes.versions' version '0.27.0'
    id 'io.franzbecker.gradle-lombok' version '3.3.0'
	id 'java'
    id 'jacoco'
    id 'com.google.cloud.tools.jib' version '2.0.0'
    id 'maven-publish'
    id 'com.github.spotbugs' version '3.0.0'
    id 'info.solidsoft.pitest' version '1.4.6'
}

gitProperties {
    dateFormat = "yyyy-MM-dd'T'HH:mmZ"
    dateFormatTimeZone = "UTC"
}

spotbugs {
    toolVersion = '4.0.0-beta1'
    ignoreFailures = true
    effort = 'max'
    reportLevel = 'low'
    showProgress = true
}

group = 'io.pivotal'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
	mavenCentral()
    maven { url "https://repo.spring.io/snapshot" }
	maven { url "https://repo.spring.io/milestone" }
    maven { url "https://dl.bintray.com/kptfh/json-reactive" }
}

configurations {
	// exclude Logback
    implementation.exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
}

dependencies {
	annotationProcessor('org.projectlombok:lombok')
    implementation('org.projectlombok:lombok')
    implementation('org.springframework.boot:spring-boot-configuration-processor')
    implementation('org.apache.commons:commons-lang3')
    implementation('com.fasterxml.jackson.datatype:jackson-datatype-jsr310')
	implementation('com.playtika.reactivefeign:feign-reactor-webclient:2.0.0')
	implementation('com.playtika.reactivefeign:feign-reactor-spring-configuration:2.0.0')
	implementation('org.springframework.boot:spring-boot-starter-webflux')
    implementation('org.springframework.boot:spring-boot-starter-log4j2')
    implementation('com.lmax:disruptor:3.4.2')
	testImplementation('org.cloudfoundry:cloudfoundry-client-reactor:4.0.1.RELEASE')
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
	testImplementation('io.projectreactor:reactor-test')
}

test {
	useJUnitPlatform()
	testLogging {
		events "passed", "skipped", "failed"
        exceptionFormat "full"
        showCauses true
        showExceptions true
        showStackTraces true
	}
}

jacoco {
	toolVersion = "0.8.5"
}

jacocoTestReport {
	reports {
		xml.enabled = true
		html.enabled = true
		csv.enabled = false
	}
}

pitest {
    testPlugin = "junit5"
    jvmArgs = ['-Xmx1024m']
    threads = 5
    targetClasses = ['io.pivotal.cfapp.*']
    outputFormats = ['XML', 'HTML']
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = 'davos-client'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
        }
        mavenJava(MavenPublication) {
            pom {
                name = 'davos-client'
                url = 'https://github.com/pacphi/davos-client'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'pacphi'
                        name = 'Chris Phillipson'
                        email = 'cphillipson@pivotal.io'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/pacphi/davos-client.git'
                    developerConnection = 'scm:git:ssh://github.com/pacphi/davos-client.git'
                    url = 'https://github.com/pacphi/davos-client'
                }
            }
        }
    }
    repositories {
        maven {
            // change URLs to point to your repos, e.g. http://my.org/repo
            def releasesRepoUrl = "$buildDir/repos/releases"
            def snapshotsRepoUrl = "$buildDir/repos/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

import io.franzbecker.gradle.lombok.task.DelombokTask

task delombok(type: DelombokTask, dependsOn: compileJava) {
    ext.outputDir = file("$buildDir/delombok")
    outputs.dir(outputDir)
    sourceSets.main.java.srcDirs.each {
        inputs.dir(it)
        args(it, "-d", outputDir)
    }
    doFirst {
        outputDir.deleteDir()
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    dependsOn delombok
    source = delombok.outputDir
    failOnError = false
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

bootJar.enabled = false
jar.enabled = true

check.dependsOn jacocoTestReport